plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
}

version = fabric_mod_version
archivesBaseName = "${mod_id}"

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings(loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_mc_version}:${parchment_version}@zip")
    })

    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    modImplementation "com.illusivesoulworks.spectrelib:spectrelib-fabric:${spectrelib_version}"
    include "com.illusivesoulworks.spectrelib:spectrelib-fabric:${spectrelib_version}"

    modImplementation("com.terraformersmc:modmenu:${mod_menu_version}") {
        transitive = false
    }

    include group: 'com.illusivesoulworks.spectrelib', name: 'spectrelib-fabric', version: "${spectrelib_version}"

    // Testing Mods
    //modRuntimeOnly "curse.maven:sleepwarp-579295:4576762"
    //modRuntimeOnly "curse.maven:fabricseasons-413523:4576886"
}

loom {
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

def changelogFile = project(":").projectDir.toString() + '/build/libs/changelog.txt'
def publishFile = project(":").projectDir.toString() + "/Fabric/build/libs/${archivesBaseName}-${version}.jar"

publishMods {
    file = file(publishFile)
    changelog = file(changelogFile).text
    type = "${release_type}".startsWith("release") ? STABLE : ("${release_type}".startsWith("beta") ? BETA : ALPHA)
    modLoaders.add('fabric')

    if (project.hasProperty('bdcurseApiKey')) {
        curseforge {
            accessToken = project.bdcurseApiKey
            projectId = project.bdcurseId
            javaVersions.add(JavaVersion.toVersion("${java_version}"))
            clientRequired = true
            serverRequired = true
            minecraftVersions.addAll("${release_versions}".split(','))
            embeds('spectrelib')
			requires('fabric-api')
			optional('fabric-seasons')
        }
    }
    if (project.hasProperty('bdModrinthId')) {
        modrinth {
            accessToken = project.modrinthKey
            projectId = project.bdModrinthId
            minecraftVersions.addAll("${release_versions}".split(','))
			requires('fabric-api')
			optional('fabric-seasons')
        }
    }
    if (project.hasProperty('bdGithubToken')) {
    }
}
